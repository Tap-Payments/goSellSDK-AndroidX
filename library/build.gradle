plugins {
    id 'com.android.library'
    id 'maven-publish'
}


group = 'com.github.Tap-Payments'
def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}
android {
    compileSdkVersion 35
    defaultConfig {
        minSdkVersion 21
        namespace "company.tap.gosellapi"
        targetSdkVersion 35
        versionCode 2
        versionName getVersionName()
        consumerProguardFiles 'proguard-rules.pro'
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
           
        }
        customDebugType {
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        buildConfig = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.code.gson:gson:2.13.1'
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    api 'com.google.android.material:material:1.12.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.recyclerview:recyclerview:1.3.2'
    api 'androidx.cardview:cardview:1.0.0'
    api'androidx.core:core-ktx:1.12.0'
    api 'com.github.Tap-Payments:TapGLKit-Android:1.19.1'
    api 'com.github.Tap-Payments:TapCardValidator-Android:2.16.2.1'



    api 'jp.wasabeef:blurry:4.0.1'


   // implementation 'cn.aigestudio.wheelpicker:WheelPicker:1.1.2'

    api 'com.github.bumptech.glide:glide:4.12.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'com.google.android.gms:play-services-wallet:19.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    api 'org.jetbrains:annotations:15.0'
    api 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    api 'com.squareup.okhttp3:okhttp:4.10.0'



    api 'androidx.multidex:multidex:2.0.1'

}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}


// Create sources and Javadoc JARs
task releaseSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

task releaseJavadoc(type: Jar) {
    archiveClassifier.set('javadoc')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {
                // Publish AAR
                artifact("$buildDir/outputs/aar/${project.name}-release.aar") {
                    builtBy tasks.named("assembleRelease")
                }

                // Attach sources and javadoc
                artifact(tasks.named("releaseSourcesJar"))
                artifact(tasks.named("releaseJavadoc"))

                groupId = "com.github.Tap-Payments"
                artifactId = "goSellSDK-AndroidX"
                version = "3.19.40"

                // Variant-aware: ensure dependencies are included in POM
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.api.allDependencies.each { dep ->
                        def depNode = dependenciesNode.appendNode('dependency')
                        depNode.appendNode('groupId', dep.group)
                        depNode.appendNode('artifactId', dep.name)
                        depNode.appendNode('version', dep.version)
                        depNode.appendNode('scope', 'compile')
                    }
                }
            }
        }
    }

    // Ensure metadata waits for sources/javadoc
    tasks.named("generateMetadataFileForReleasePublication") {
        dependsOn(tasks.named("releaseSourcesJar"))
        dependsOn(tasks.named("releaseJavadoc"))
    }
}